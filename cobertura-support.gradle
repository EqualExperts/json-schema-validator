def coberturaRuntime = 'net.sourceforge.cobertura:cobertura:1.9.4.1'

configurations {
    testRuntime
}
dependencies {
    testRuntime coberturaRuntime
}

task coberturaReports {
    if (!gradle.gradleVersion.startsWith('0.9.')) {
        ext {
            codeCoverageEnabled = null
            reportInformation = null
        }
    }
}

coberturaReports.codeCoverageEnabled = System.getProperty("coverage")
coberturaReports.reportInformation = []

if (coberturaReports.codeCoverageEnabled) {
    if (!project.hasProperty("build")) {
        task build
    }
    build.dependsOn(coberturaReports)
}

allprojects {
    if (!gradle.gradleVersion.startsWith('0.9.')) {
        ext.reportCodeCoverageIfEnabled = null
    }
    reportCodeCoverageIfEnabled = {
        if (!coberturaReports.codeCoverageEnabled) {
            return
        }

        rootProject.coberturaReports.dependsOn(test)

        def mainClassesDirectory
        if (!gradle.gradleVersion.startsWith('0.9.')) {
            mainClassesDirectory = sourceSets['main'].output.classesDir
        } else {
            mainClassesDirectory = sourceSets.main.classesDir
        }

        def backupOfUninstrumentedClasses = "${mainClassesDirectory}-copy"
        def coberturaDataFile = "${project.buildDir}/cobertura.ser"

        def srcDirs
        if (!gradle.gradleVersion.startsWith('0.9.')) {
            srcDirs = sourceSets.main.allSource.srcDirs
        } else {
            srcDirs = sourceSets.main.java.srcDirs
            if (sourceSets.main.hasProperty('groovy')) {
                srcDirs += sourceSets.main.groovy.srcDirs
            }
            if (sourceSets.main.hasProperty('scala')) {
                srcDirs += sourceSets.main.scala.srcDirs
            }
        }

        rootProject.coberturaReports.reportInformation << [sourceDirs: srcDirs, dataFile: coberturaDataFile]

        dependencies {
            testRuntime coberturaRuntime
        }

        test.doFirst {
            if (mainClassesDirectory.exists()) {
                ant {
                    delete(file: coberturaDataFile, failonerror: false)
                    delete(dir: backupOfUninstrumentedClasses, failonerror: false)
                    copy(todir: backupOfUninstrumentedClasses) {
                        fileset(dir: mainClassesDirectory)
                    }
                    taskdef(resource: 'tasks.properties', classpath: configurations.testRuntime.asPath)
                    'cobertura-instrument'(datafile: coberturaDataFile) {
                        fileset(dir: mainClassesDirectory, includes: "**/*.class")
                    }
                }
            }
        }

        test.doLast {
            if (new File(backupOfUninstrumentedClasses).exists()) {
                ant {
                    delete(file: mainClassesDirectory)
                    move(file: backupOfUninstrumentedClasses, tofile: mainClassesDirectory)
                }
            }
        }

        test {
            systemProperties["net.sourceforge.cobertura.datafile"] = coberturaDataFile
        }
    }
}

coberturaReports << {

    if (coberturaReports.reportInformation.empty) {
        throw new RuntimeException("No cobertura projects enabled!")
    }

    println "Generating code coverage report"
    def buildDirName = project.buildDir.canonicalPath
    def mergedSerFile = "${project.buildDir}/cobertura-merged.ser"

    ant {
        delete(file: mergedSerFile)
        taskdef(resource: 'tasks.properties', classpath: configurations.testRuntime.asPath)
        'cobertura-merge'(datafile: mergedSerFile) {
            coberturaReports.reportInformation.each {
                if (new File(it.dataFile.toString()).exists()) {
                    fileset(file: it.dataFile)
                }
            }
        }
        'cobertura-report'(destdir: "${buildDirName}/reports/coverage", format: System.getProperty("coverage"), datafile: mergedSerFile) {
            coberturaReports.reportInformation.each {
                it.sourceDirs.each {
                    if (new File(it.toString()).exists()) {
                        fileset(dir: it, includes: "**/*.*")
                    }
                }
            }
        }
    }
}
